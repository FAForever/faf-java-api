buildscript {
  repositories {
    jcenter()
    mavenCentral()
    maven { url 'http://repo.spring.io/plugins-release' }
  }
  dependencies {
    classpath("com.bmuschko:gradle-docker-plugin:${gradleDockerVersion}")
    classpath("org.springframework.build.gradle:propdeps-plugin:${propdepsVersion}")
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("org.kt3k.gradle.plugin:coveralls-gradle-plugin:${coverallsGradlePluginVersion}")
  }
}

// source: https://github.com/mendhak/Gradle-Travis-Colored-Output/blob/master/ColoredOutput.gradle
tasks.withType(Test) {

  boolean TRAVIS_FOLDING = project.hasProperty('TRAVIS_FOLDING') ? project.TRAVIS_FOLDING : true
  String ANSI_BOLD_WHITE = "\u001B[0;1m";
  String ANSI_RESET = "\u001B[0m";
  String ANSI_BLACK = "\u001B[30m";
  String ANSI_RED = "\u001B[31m";
  String ANSI_GREEN = "\u001B[32m";
  String ANSI_YELLOW = "\u001B[33m";
  String ANSI_BLUE = "\u001B[34m";
  String ANSI_PURPLE = "\u001B[35m";
  String ANSI_CYAN = "\u001B[36m";
  String ANSI_WHITE = "\u001B[37m";
  String CHECK_MARK = "\u2713";
  String NEUTRAL_FACE = "\u0CA0_\u0CA0";
  String X_MARK = "\u274C";

  beforeSuite { suite ->
    if (suite.name.startsWith("Test Run") || suite.name.startsWith("Gradle Worker")) {
      return;
    }

    if (suite.parent != null && suite.className != null) {
      if (TRAVIS_FOLDING) {
        out.println("travis_fold:start:" + suite.name + "\r");
      }
      out.println(ANSI_BOLD_WHITE + suite.name + ANSI_RESET);
    }

  }

  afterTest { descriptor, result ->
    def indicator = ANSI_WHITE

    if (result.failedTestCount > 0) {
      indicator = ANSI_RED + X_MARK;
    } else if (result.skippedTestCount > 0) {
      indicator = ANSI_YELLOW + NEUTRAL_FACE;
    } else {
      indicator = ANSI_GREEN + CHECK_MARK;
    }

    out.println('    ' + indicator + ANSI_RESET + " " + descriptor.name);

    if (result.failedTestCount > 0) {
      out.println(' ');
    }
  }

  afterSuite { desc, result ->
    if (desc.parent != null && desc.className != null) {

      if (TRAVIS_FOLDING && result.failedTestCount == 0) {
        out.println("travis_fold:end:" + desc.name + "\r");
      }
      out.println("");
    }

    if (!desc.parent) { // will match the outermost suite
      def failStyle = ANSI_RED;
      def skipStyle = ANSI_YELLOW;
      def summaryStyle = ANSI_WHITE;
      if (result.failedTestCount > 0) {
        failStyle = ANSI_RED;
      }
      if (result.skippedTestCount > 0) {
        skipStyle = ANSI_YELLOW;
      }

      switch (result.resultType) {
        case TestResult.ResultType.SUCCESS:
          summaryStyle = ANSI_GREEN;
          break;
        case TestResult.ResultType.FAILURE:
          summaryStyle = ANSI_RED;
          break;
      }

      out.println("--------------------------------------------------------------------------");
      out.println("Results: " + summaryStyle + "${result.resultType}" + ANSI_RESET
        + " (${result.testCount} tests, "
        + ANSI_GREEN + "${result.successfulTestCount} passed" + ANSI_RESET
        + ", " + failStyle + "${result.failedTestCount} failed" + ANSI_RESET
        + ", " + skipStyle + "${result.skippedTestCount} skipped" + ANSI_RESET
        + ")");
      out.println("--------------------------------------------------------------------------");
    }
  }

}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'propdeps'

group = 'faforever'
version = '0.9.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
  maven { url "http://repo.jenkins-ci.org/public/" }
  maven { url "https://jitpack.io" }
  maven { url "http://dl.bintray.com/typesafe/maven-releases" }
}

compileJava.dependsOn(processResources)
task wrapper(type: Wrapper) {
  gradleVersion = '3.2.1'
}

configurations {
  compile.exclude module: "spring-boot-starter-tomcat"
  compile.exclude module: "tomcat-jdbc"
  compile.exclude module: "assertj-core"
}

processResources {
  filesMatching('**/application.yml') {
    filter {
      it.replace('#faf-api.version#', project.version.toString())
    }
  }
}

// JACOCO & COVERALLS

apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
jacocoTestReport {
  reports {
    xml.enabled = true
  }
}

// CODACY

configurations {
  codacy
}

task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
  main = "com.codacy.CodacyCoverageReporter"
  classpath = configurations.codacy
  args = ["-l", "Java", "-r", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"]
}

// DOCKER

apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'application'

import com.bmuschko.gradle.docker.DockerRemoteApiPlugin
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

applicationName = rootProject.name
docker {
  if (project.hasProperty('dockerHost')) {
    url = dockerHost
  }

  registryCredentials {
    if (project.hasProperty('dockerUsername') && project.hasProperty('dockerPassword')) {
      username = dockerUsername
      password = dockerPassword
    }
  }
}

task dockerCopyDistResources(type: Copy, dependsOn: distTar) {
  group = DockerRemoteApiPlugin.DEFAULT_TASK_GROUP
  description = "Copies the distribution resources to a temporary directory for image creation."

  from { distTar.archivePath }
  from 'src/main/docker/default-cmd.sh'
  into { createDockerfile.destFile.parentFile }
}

task createDockerfile(type: Dockerfile, dependsOn: dockerCopyDistResources) {
  group = DockerRemoteApiPlugin.DEFAULT_TASK_GROUP
  destFile = file("${buildDir}/docker/Dockerfile")
  from 'frolvlad/alpine-oraclejdk8:slim'
  maintainer 'Michel Jung "michel.jung89@gmail.com"'

  environmentVariable 'user', 'root'

  user '${user}'

  volume '/tmp'
  addFile 'default-cmd.sh', 'default-cmd.sh'
  addFile({ distTar.archivePath.name }, { '/' })

  runCommand 'chmod +x default-cmd.sh'

  exposePort 8010

  defaultCommand './default-cmd.sh', "/${project.name}-${project.version}/bin/${project.name}"
}

task buildDockerImage(type: DockerBuildImage, dependsOn: createDockerfile) {
  inputDir file(createDockerfile.destFile.parentFile)
  tag "${project.group}/${rootProject.name}:${project.version}"
}

task pushDockerImage(type: DockerPushImage, dependsOn: buildDockerImage) {
  group = DockerRemoteApiPlugin.DEFAULT_TASK_GROUP
  description = "Publishes the created docker image tagged with the project's version."

  imageName "${project.group}/${rootProject.name}"
  tag project.version
}

configurations.all {
  // Cache -SNAPSHOT for 60 seconds only
  resolutionStrategy.cacheChangingModulesFor 60, 'seconds'
}

dependencyManagement {
  dependencies {
    dependency("org.hibernate:hibernate-core:${hibernateVersion}")
    dependency("org.hibernate:hibernate-entitymanager:${hibernateVersion}")
    dependency("org.hibernate:hibernate-validator:${hibernateVersion}")
    dependency("org.mockito:mockito-core:${mockitoVersion}")
  }
}

dependencies {
  compile("org.springframework.boot:spring-boot-starter-actuator")
  compile("org.springframework.boot:spring-boot-starter-jdbc")
  compile("org.springframework.boot:spring-boot-starter-data-jpa")
  compile("org.springframework.boot:spring-boot-starter-web")
  compile("org.springframework.boot:spring-boot-starter-jetty")
  compile("org.springframework.boot:spring-boot-starter-security")
  compile("org.springframework.boot:spring-boot-starter-thymeleaf")
  compile("de.codecentric:spring-boot-admin-starter-client:${springBootAdminClientVersion}")

  compile("com.github.FAForever:faf-java-commons:${fafCommonsVersion}")
  compile("org.kohsuke:github-api:${githubApiVersion}")
  compile("org.jolokia:jolokia-core")
  compile("org.springframework.security:spring-security-jwt:${springSecurityJwtVersion}")
  compile("org.springframework.security.oauth:spring-security-oauth2:${springSecurityOauth2Version}")
  compile("org.springframework:spring-context-support:${springContextSupportVersion}")
  compile("org.eclipse.jgit:org.eclipse.jgit:${jgitVersionn}")
  compile("org.jetbrains:annotations:${jetbrainsAnnotationsVersion}")
  compile("com.google.guava:guava:${guavaVersion}")
  compile("io.springfox:springfox-swagger-ui:${swaggerVersion}")
  compile("io.springfox:springfox-swagger2:${swaggerVersion}")
  compile("io.swagger:swagger-core:${swaggerCoreVersion}")
  compile("javax.inject:javax.inject:${javaxInjectVersion}")
  compile("com.yahoo.elide:elide-core:${elideVersion}")
  compile("com.yahoo.elide:elide-swagger:${elideVersion}")
  compile("com.yahoo.elide:elide-datastore-hibernate5:${elideVersion}")
  compile("org.hibernate:hibernate-java8:${hibernateVersion}")
  compile("com.zaxxer:HikariCP:${hikariCpVersion}") {
    exclude(module: 'tools')
  }

  compile("org.luaj:luaj-jse:${luajVersion}")
  compile("com.github.micheljung:nocatch:${nocatchVersion}")
  compile("junit-addons:junit-addons:${junitAddonsVersion}")
  compile("com.googlecode.zohhak:zohhak:${zohhakVersion}")
  compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonDatatypeJsr310Version}")
  compile("com.mandrillapp.wrapper.lutung:lutung:${lutungVersion}")
  compile("org.apache.commons:commons-compress:${commonsCompressVersion}")
  compile("io.prometheus:simpleclient_hotspot:${prometheusVersion}")
  compile("io.prometheus:simpleclient_servlet:${prometheusVersion}")
  compile("io.prometheus:simpleclient_spring_boot:${prometheusVersion}")

  runtime("mysql:mysql-connector-java:${mysqlConnectorVersion}")

  optional("org.springframework.boot:spring-boot-configuration-processor")

  testCompile("org.springframework.boot:spring-boot-starter-test")
  testCompile("org.springframework.restdocs:spring-restdocs-mockmvc")
  testCompile("org.springframework.security:spring-security-test")
  testCompile("com.h2database:h2:${h2Version}")
  testCompile("com.jayway.jsonpath:json-path:${jsonPath}")
  testCompile("com.jayway.jsonpath:json-path-assert:${jsonPathAssert}")

  codacy("com.github.codacy:codacy-coverage-reporter:-SNAPSHOT")
}

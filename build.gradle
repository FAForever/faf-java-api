buildscript {
  repositories {
    jcenter()
    mavenCentral()
    maven { url 'http://repo.spring.io/plugins-release' }
  }
  dependencies {
    classpath("org.springframework.build.gradle:propdeps-plugin:${propdepsVersion}")
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("org.kt3k.gradle.plugin:coveralls-gradle-plugin:${coverallsGradlePluginVersion}")
  }
}

// source: https://github.com/mendhak/Gradle-Travis-Colored-Output/blob/master/ColoredOutput.gradle
tasks.withType(Test) {

  boolean TRAVIS_FOLDING = project.hasProperty('TRAVIS_FOLDING') ? project.TRAVIS_FOLDING : true
  String ANSI_BOLD_WHITE = "\u001B[0;1m";
  String ANSI_RESET = "\u001B[0m";
  String ANSI_BLACK = "\u001B[30m";
  String ANSI_RED = "\u001B[31m";
  String ANSI_GREEN = "\u001B[32m";
  String ANSI_YELLOW = "\u001B[33m";
  String ANSI_BLUE = "\u001B[34m";
  String ANSI_PURPLE = "\u001B[35m";
  String ANSI_CYAN = "\u001B[36m";
  String ANSI_WHITE = "\u001B[37m";
  String CHECK_MARK = "\u2713";
  String NEUTRAL_FACE = "\u0CA0_\u0CA0";
  String X_MARK = "\u274C";

  beforeSuite { suite ->
    if (suite.name.startsWith("Test Run") || suite.name.startsWith("Gradle Worker")) {
      return;
    }

    if (suite.parent != null && suite.className != null) {
      if (TRAVIS_FOLDING) {
        out.println("travis_fold:start:" + suite.name + "\r");
      }
      out.println(ANSI_BOLD_WHITE + suite.name + ANSI_RESET);
    }

  }

  afterTest { descriptor, result ->
    def indicator = ANSI_WHITE

    if (result.failedTestCount > 0) {
      indicator = ANSI_RED + X_MARK;
    } else if (result.skippedTestCount > 0) {
      indicator = ANSI_YELLOW + NEUTRAL_FACE;
    } else {
      indicator = ANSI_GREEN + CHECK_MARK;
    }

    out.println('    ' + indicator + ANSI_RESET + " " + descriptor.name);

    if (result.failedTestCount > 0) {
      out.println(' ');
    }
  }

  afterSuite { desc, result ->
    if (desc.parent != null && desc.className != null) {

      if (TRAVIS_FOLDING && result.failedTestCount == 0) {
        out.println("travis_fold:end:" + desc.name + "\r");
      }
      out.println("");
    }

    if (!desc.parent) { // will match the outermost suite
      def failStyle = ANSI_RED;
      def skipStyle = ANSI_YELLOW;
      def summaryStyle = ANSI_WHITE;
      if (result.failedTestCount > 0) {
        failStyle = ANSI_RED;
      }
      if (result.skippedTestCount > 0) {
        skipStyle = ANSI_YELLOW;
      }

      switch (result.resultType) {
        case TestResult.ResultType.SUCCESS:
          summaryStyle = ANSI_GREEN;
          break;
        case TestResult.ResultType.FAILURE:
          summaryStyle = ANSI_RED;
          break;
      }

      out.println("--------------------------------------------------------------------------");
      out.println("Results: " + summaryStyle + "${result.resultType}" + ANSI_RESET
        + " (${result.testCount} tests, "
        + ANSI_GREEN + "${result.successfulTestCount} passed" + ANSI_RESET
        + ", " + failStyle + "${result.failedTestCount} failed" + ANSI_RESET
        + ", " + skipStyle + "${result.skippedTestCount} skipped" + ANSI_RESET
        + ")");
      out.println("--------------------------------------------------------------------------");
    }
  }

}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'propdeps'
apply plugin: 'idea'

group = 'faforever'
version = 'snapshot'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
  mavenCentral()
  maven { url "http://repo.jenkins-ci.org/public/" }
  maven { url "https://jitpack.io" }
  maven { url "http://dl.bintray.com/typesafe/maven-releases" }
}

compileJava.dependsOn(processResources)

configurations {
  compile.exclude module: "spring-boot-starter-tomcat"
  compile.exclude module: "tomcat-jdbc"
  compile.exclude module: "assertj-core"
}

idea {
  module {
    testSourceDirs += file('src/inttest/java')
  }
}

processResources {
  filesMatching('**/application.yml') {
    filter {
      it.replace('#faf-api.version#', project.version.toString())
    }
  }
}

sourceSets {
  inttest {
    java.srcDir 'src/inttest/java'
    resources.srcDir 'src/inttest/resources'
    compileClasspath += sourceSets.test.compileClasspath
    runtimeClasspath += sourceSets.test.runtimeClasspath
  }
}

configurations {
  inttestCompile.extendsFrom testCompile
  inttestRuntime.extendsFrom testRuntime
}


task inttest(type: Test) {
  group = LifecycleBasePlugin.VERIFICATION_GROUP
  description = "Runs the integration tests."

  setTestClassesDirs(files(sourceSets.inttest.output))
  classpath = sourceSets.inttest.runtimeClasspath

  useJUnitPlatform()

  mustRunAfter test
}

// JACOCO & COVERALLS

apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
jacocoTestReport {
  executionData.from = files(["$buildDir/jacoco/test.exec", "$buildDir/jacoco/inttest.exec"])
  reports {
    xml.enabled = true
  }
}

// CODACY

configurations {
  codacy
}

task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
  main = "com.codacy.CodacyCoverageReporter"
  classpath = configurations.codacy
  args = ["report", "-l", "Java", "-r", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"]
}

configurations.all {
  // Cache -SNAPSHOT for 60 seconds only
  resolutionStrategy.cacheChangingModulesFor 60, 'seconds'
}

build.dependsOn inttest

dependencyManagement {
  dependencies {
    dependency("org.mockito:mockito-core:${mockitoVersion}")
  }
}

test {
  useJUnitPlatform()
  testLogging {
    events("passed", "skipped", "failed")
  }
}

dependencies {
  compileOnly("org.projectlombok:lombok:${lombokVersion}")
  annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
  testAnnotationProcessor("org.projectlombok:lombok:${lombokVersion}")
  annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
  testAnnotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

  //This is needed, because something in regards prometheus adds this annotationProcessor to the classpath
  //See https://github.com/jenkinsci/prometheus-plugin/issues/35
  annotationProcessor("org.jvnet.hudson:annotation-indexer:1.2")
  testAnnotationProcessor("org.jvnet.hudson:annotation-indexer:1.2")

  runtime("io.micrometer:micrometer-registry-prometheus")

  compile("org.springframework.boot:spring-boot-starter-jdbc")
  compile("org.springframework.boot:spring-boot-starter-data-jpa")
  compile("org.springframework.boot:spring-boot-starter-web")
  compile("org.springframework.boot:spring-boot-starter-jetty")
  compile("org.springframework.boot:spring-boot-starter-security")
  compile("org.springframework.boot:spring-boot-starter-thymeleaf")
  compile("org.springframework.boot:spring-boot-starter-mail")
  compile("de.codecentric:spring-boot-admin-starter-client:${springBootAdminClientVersion}")

  compile("com.github.ben-manes.caffeine:caffeine")
  compile("com.github.FAForever:faf-java-commons:${fafCommonsVersion}")
  compile("org.kohsuke:github-api:${githubApiVersion}")
  compile("org.jolokia:jolokia-core")
  compile("org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:${springSecurityOauth2AutoConfigureVersion}")
  compile("org.springframework.security.oauth:spring-security-oauth2:${springSecurityOauth2Version}")
  compile("org.springframework.security:spring-security-jwt:${springSecurityJwtVersion}")
  compile("org.eclipse.jgit:org.eclipse.jgit:${jgitVersionn}")
  compile("org.jetbrains:annotations:${jetbrainsAnnotationsVersion}")
  compile("com.google.guava:guava:${guavaVersion}")
  compile("io.springfox:springfox-swagger-ui:${swaggerVersion}")
  compile("io.springfox:springfox-swagger2:${swaggerVersion}")
  compile("io.swagger:swagger-core:${swaggerCoreVersion}")
  compile("javax.inject:javax.inject:${javaxInjectVersion}")
  // When switching from Java 8 to 9, I got "class file for javax.interceptor.interceptorbinding not found". Adding this fixed it, but IDK what caused it.
  compile("javax.interceptor:javax.interceptor-api:${javaxInterceptorApiVersion}")
  compile("com.yahoo.elide:elide-core:${elideVersion}")
  compile("com.yahoo.elide:elide-swagger:${elideVersion}")
  compile("com.yahoo.elide:elide-datastore-hibernate5:${elideVersion}")
//  compile("org.glassfish.jersey.core:jersey-common:2.26")

  compile("org.luaj:luaj-jse:${luajVersion}")
  compile("com.github.micheljung:nocatch:${nocatchVersion}")
  compile("junit-addons:junit-addons:${junitAddonsVersion}")
  compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonDatatypeJsr310Version}")
  compile("com.mandrillapp.wrapper.lutung:lutung:${lutungVersion}")
  compile("org.apache.commons:commons-compress:${commonsCompressVersion}")
  compile("org.json:json:${jsonVersion}")

  compile("com.github.jasminb:jsonapi-converter:${jsonapiConverterVersion}")
  runtime("mysql:mysql-connector-java:${mysqlConnectorVersion}")

  optional("org.springframework.boot:spring-boot-configuration-processor")

  testCompile("org.springframework.boot:spring-boot-starter-test")
  testCompile("org.springframework.restdocs:spring-restdocs-mockmvc")
  testCompile("org.springframework.security:spring-security-test")
  testImplementation("org.junit.jupiter:junit-jupiter:${jUnit5Version}")
  testImplementation("org.junit.jupiter:junit-jupiter-params:${jUnit5Version}")
  testImplementation("org.mockito:mockito-junit-jupiter:${mockitoVersion}")
  testImplementation("org.junit.jupiter:junit-jupiter-migrationsupport:${jUnit5Version}")
  testRuntimeOnly("org.junit.vintage:junit-vintage-engine:${jUnit5Version}")
  testCompile("com.h2database:h2:${h2Version}")
  testCompile("com.jayway.jsonpath:json-path:${jsonPath}")
  testCompile("com.jayway.jsonpath:json-path-assert:${jsonPathAssert}")

  codacy("com.github.codacy:codacy-coverage-reporter:${codacyCoverageReporterVersion}")
}

ext["mockito.version"] = "${mockitoVersion}"
ext["junit-jupiter.version"] = "${jUnit5Version}"
